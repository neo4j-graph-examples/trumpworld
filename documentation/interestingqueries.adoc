= Interesting queries in the TrumpWorld graph


== What are the most connected organizations? What about the least?

[source,cypher]
----
MATCH (o:Organization)-[r]-()
RETURN o.name, count(*), collect(distinct type(r)) AS types
ORDER BY count(*) DESC
LIMIT 5
----

== Second degree Kushner network

[source,cypher]
----
MATCH network = (:Person {name:"JARED KUSHNER"})-[*..2]-()
RETURN network
----


== What role do banks play in the Trump ecosystem

[source,cypher]
----
MATCH (bank:Organization)--(other)
WHERE bank.name contains "BANK"
RETURN *
----


== What's the connection between Donald Trump and Vladimir Putin

[source,cypher]
----
MATCH (vp:Person {name:"VLADIMIR PUTIN"}),(dt:Person {name:"DONALD J. TRUMP"})
MATCH path = allShortestPaths( (vp)-[*]-(dt) )
RETURN path
----

== Nominees in Trump's cabinet

[source,cypher]
----
MATCH (p:Person)-[n:NOMINEE]->()
RETURN p.name, n.connection
ORDER BY split(p.name," ")[-1] DESC LIMIT 5
----

A nominee's graph of influence

[source,cypher]
----
MATCH path = (o:Organization)<-[:INVOLVED_WITH]-(p:Person)-[n:NOMINEE]->(djt)
RETURN path
----


== What does people in Trump's network share with other people

[source,cypher]
----
MATCH (p:Person) WITH p LIMIT 10
MATCH (p)-[r1]-(o:Organization)-[r2]-(someoneElse:Person)
WHERE NOT (p)--(someoneElse) AND p <> someoneElse
RETURN p.name, someoneElse.name, count(distinct o) AS ct, collect(distinct o.name) AS orgs
ORDER BY ct desc
----

== Social Network recommendation style query

[source,cypher]
----
MATCH (p:Person) WITH p LIMIT 200
MATCH (p)-[r1]-(o:Organization)-[r2]-(someoneElse:Person)-[r3]-(otherorg:Organization)
WHERE NOT (p)--(someoneElse) AND NOT (p)--(otherorg) AND p <> someoneElse AND o <> otherorg
RETURN p.name, count(distinct someoneElse) AS ct, otherorg.name
ORDER BY ct desc
----



